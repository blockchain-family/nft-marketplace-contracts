pragma ever-solidity >= 0.62.0;

import '../errors/BaseErrors.tsol';

import "../structures/IGasValueStructure.tsol";

import "../structures/IMarketFeeStructure.tsol";
import "../structures/IMarketBurnFeeStructure.tsol";
import "../structures/IDiscountCollectionsStructure.tsol";
import "../interfaces/IEventsCollectionsSpecialRules.tsol";
import "../modules/access/OwnableInternal.tsol";
import "../interfaces/IEventsMarketFee.tsol";
import "./TargetBalance.tsol";
import "./BaseOffer.tsol";
import "../interfaces/IOffer.tsol";


abstract contract BaseRoot is
    OwnableInternal,
    TargetBalance,
    IGasValueStructure,
    IEventsMarketFee,
    IDiscountCollectionsStructure,
    IEventsCollectionsSpecialRules
{
    MarketFee private fee_;
    optional(MarketBurnFee) private burnFee_;


    address private wnativeRoot_;

    TvmCell private offerCode_;
    uint32 private currentVersionOffer_;

    mapping(address => CollectionFeeInfo) private collectionsSpecialRules_;

    modifier reserve() {
        tvm.rawReserve(_getTargetBalanceInternal(), 0);
        _;
    }

    function _getTargetBalanceInternal()
        internal
        view
        virtual
        override
        returns (uint128);


    function _initialization(
        MarketFee _fee,
        address _wnativeRoot
    )
        internal
        virtual
    {
        _setMarketFee(_fee);
        wnativeRoot_ = _wnativeRoot;
    }

    function activateChildContract(
        address _offer
    )
        external
        onlyOwner
    {
        BaseOffer(_offer).activate{value: 0, flag: 64, bounce: false}();
    }

    function finishChildContract(
        address _offer,
        uint32 _callbackId
    )
        external
        onlyOwner
    {
        IOffer(_offer).finishOffer{value: 0, flag: 64, bounce: false}(msg.sender, _callbackId);
    }


// market fee
    function _setMarketFee(
        MarketFee _fee
    )
        internal
        virtual
    {
        require(_fee.denominator > 0, BaseErrors.denominator_not_be_zero);
        fee_ = _fee;
        emit MarketFeeDefaultChanged(fee_);
    }

    function _getMarketFee()
        internal
        view
        virtual
        returns (MarketFee)
    {
        return fee_;
    }

// market burn fee

    function _setMarketBurnFee(
        MarketBurnFee _fee
    )
        internal
        virtual
    {
        burnFee_ = _fee;
        emit MarketBurnFeeDefaultChanged(_fee);
    }

    function _getMarketBurnFee()
        internal
        view
        virtual
        returns (optional(MarketBurnFee))
    {
        return burnFee_;
    }

// support native token
    function _getWnativeRoot()
        internal
        view
        virtual
        returns (address)
    {
        return wnativeRoot_;
    }

// discount
    function _getCollectionsSpecialRules()
        internal
        view
        virtual
        returns (mapping(address => CollectionFeeInfo))
    {
        return collectionsSpecialRules_;
    }

    function _setCollectionsSpecialRules(
        address _collection,
        CollectionFeeInfo _collectionFeeInfo
    )
        internal
        virtual
    {
        collectionsSpecialRules_[_collection] = _collectionFeeInfo;
        emit AddCollectionRules(_collection, _collectionFeeInfo);
    }

    function _deleteCollectionsSpecialRules(
        address _collection
    )
        internal
        virtual
    {
        delete collectionsSpecialRules_[_collection];
        emit RemoveCollectionRules(_collection);
    }

// upgradable
    function _getOfferCode()
        internal
        view
        virtual
        returns (TvmCell)
    {
        return offerCode_;
    }

    function _getCurrentVersionOffer()
        internal
        view
        virtual
        returns (uint32)
    {
        return currentVersionOffer_;
    }

    function _setOfferCode(
        TvmCell _newCode
    )
        internal
        virtual
    {
        offerCode_ = _newCode;
        currentVersionOffer_++;
    }

    function calcValue(
        GasValues value
    )
        internal
        pure
        returns(uint128)
    {
        return value.fixedValue + gasToValue(value.dynamicGas, address(this).wid);
    }

}
