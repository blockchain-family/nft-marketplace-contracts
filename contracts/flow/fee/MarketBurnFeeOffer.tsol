pragma ever-solidity >= 0.62.0;

import "./MarketFeeOffer.tsol";
import "../../structures/IMarketBurnFeeStructure.tsol";
import "../../interfaces/IBurnTip3.tsol";
import "../../libraries/Gas.tsol";

import "tip3/contracts/interfaces/IBurnableTokenWallet.tsol";
import "../../libraries/OperationType.tsol";
import "../native_token/SupportNativeTokenOffer.tsol";

abstract contract MarketBurnFeeOffer is MarketFeeOffer {

    function setMarketBurnFee(
        MarketBurnFee _fee,
        address _remainingGasTo
    )
        external
        onlyMarketRoot
        reserve
    {
        _setMarketBurnFee(_fee);
        _remainingGasTo.transfer({ value: 0, flag: 128 + 2, bounce: false });
    }

    function marketBurnFee()
        external
        view
        returns (optional(MarketBurnFee))
    {
        return _getMarketBurnFee();
    }

    function _retentionMarketFee(
        address _tokenWallet,
        uint128 _extraGasValue,
        uint128 _deployWalletGrams,
        uint128 _feeAmount,
        address _remainingGasTo
    )
        internal
        override
        view
    {
        TvmCell emptyPayload;
        optional(MarketBurnFee) burnFee = _getMarketBurnFee();
        if (burnFee.hasValue()) {
            uint128 burnFeeAmount = math.muldivc(_feeAmount, burnFee.get().numerator, burnFee.get().denominator);
            emit MarketFeeWithheld(_feeAmount - burnFeeAmount, _getPaymentToken());
            ITokenWallet(_tokenWallet).transfer{
                value: _deployWalletGrams + _extraGasValue,
                flag: 0,
                bounce: false
            }(
                _feeAmount - burnFeeAmount,
                _getMarketRootAddress(),
                _deployWalletGrams,
                _remainingGasTo,
                false,
                emptyPayload
            );

            TvmBuilder builder;
            builder.store(OperationType.Burn);
            builder.store(_remainingGasTo);

            IBurnableTokenWallet(_tokenWallet).burn{
                value: _extraGasValue,
                flag: 0,
                bounce: false
            }(
                burnFeeAmount,
                _getMarketRootAddress(),
                address(this),
                builder.toCell()
            );
        } else {
            emit MarketFeeWithheld(_feeAmount, _getPaymentToken());
            ITokenWallet(_tokenWallet).transfer{
                value: _deployWalletGrams + _extraGasValue,
                flag: 0,
                bounce: false
            }(
                _feeAmount,
                _getMarketRootAddress(),
                _deployWalletGrams,
                _remainingGasTo,
                false,
                emptyPayload
            );

         }

    }

    function _tokensBurn(
        uint128 amount,
        address user,
        TvmCell payload
    )
         internal
         reserve
    {
        optional(MarketBurnFee) burnFee = _getMarketBurnFee();

        uint8 operationType;
        address remainingGasTo;
        TvmSlice payloadSlice = payload.toSlice();
        if (payloadSlice.bits() >= 8) {
            operationType = payloadSlice.decode(uint8);
        }
        if (payloadSlice.bits() >= 256) {
            remainingGasTo = payloadSlice.decode(address);
        }

        if (operationType == OperationType.Transfer) {
            if (user == remainingGasTo) {
                user.transfer({ value: 0, flag: 128 + 2, bounce: false });
            } else {
                user.transfer({ value: amount, flag: 1, bounce: false });
                remainingGasTo.transfer({ value: 0, flag: 128 + 2, bounce: false });
            }
        } else if (operationType == OperationType.Burn) {
            emit MarketFeeBurn(address(this), burnFee.get().burnRecipient, burnFee.get().project);
            IBurnTip3(burnFee.get().burnRecipient).burn{
                value: 0,
                flag: 128 + 2,
                bounce: false
            }(_getOwner(), burnFee.get().project);
        } else {
            remainingGasTo.transfer({ value: 0, flag: 128 + 2, bounce: false });
        }

    }

}
